buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'org.ildar.gradleseparatetests'
version '1.0-SNAPSHOT'
apply plugin: 'kotlin'

repositories {

    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'junit:junit:4.12'
}


sourceSets {
    integrationTest {
        kotlin {
            srcDir "src/integration-test/kotlin"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task printInfo(group: "help") {
    doLast {
        sourceSets.findByName("integrationTest").runtimeClasspath.files.forEach {
            println "file : $it"
        }


        integrationTest.getTestClassesDirs().forEach {
            println "file : $it"
        }


        configurations.testRuntime.each { println it }
        //println  sourceSets.findByName("integrationTest").class
    }

}

